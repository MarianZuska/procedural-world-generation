#pragma kernel CSMain 
#include "./Noise.compute"
#include "./MarchTables.compute"

int numPointsPerAxis;

float surfaceLevel;
float perlinZoom;
float3 minSectorPos;
float3 minCornerPos;
float3 offsetVec;
float worldZoom;

int sphereCount;
int sphereRadius;

struct Triangle {
	float3 vertexC;
	float3 vertexB;
	float3 vertexA;
};

struct Sphere {
	float3 position;
	int surfaceValue;
	int index;
};

AppendStructuredBuffer<Triangle> triangles;
StructuredBuffer<Sphere> spheres;
RWStructuredBuffer<int> numTris;

//checks if the given point is inside on of the spheres
int collidingSphere(float3 pos) {
	if(sphereCount > 0) {
		for(int i=sphereCount-1; i>=0; i--) {
			Sphere sphere = spheres[i];
			float3 spos = sphere.position;
			float distToCircle2 = (pos.x - spos.x)*(pos.x - spos.x) + (pos.y - spos.y)*(pos.y - spos.y) + (pos.z - spos.z)*(pos.z - spos.z);
			float sphereRadius2 = sphereRadius * sphereRadius;

			if (distToCircle2 <= sphereRadius2) {
				return sphere.surfaceValue;
			}
		}
	}

	return -2;
}

//samples a point in 3d space, returns surface value
float samplePoint(float3 pos) {
	int sphereVal = collidingSphere(pos);
	if(sphereVal > -2) {
		return sphereVal;
	} else {
		pos /= numPointsPerAxis;
		pos *= perlinZoom;
		pos += offsetVec;
		return snoise(pos);
	}
}

//index of the curent corner configuration in the marchtable
int index(float3 pos) {
	int cubeIndex = 0;

	for (int i = 0; i < 8; i++) {
		float val = samplePoint(pos + cornerOffsets[i]);
		if (val < surfaceLevel) {
			//set i'th bit if below surface
			cubeIndex |= 1 << i;
		}
	}
	return cubeIndex;
}

[numthreads(8,8,8)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	int x = id.x;
	int y = id.y;
	int z = id.z;

	minCornerPos = minSectorPos + float3(x, y, z);

	//lookup table index
	int lookupIndex = index(minCornerPos);

	//iterate through triangleTable
	int nextVertIndex = 0;
	while (triangleTable[lookupIndex][nextVertIndex] != -1) {

		//append single triangle
		Triangle tri;
		for (int i = 0; i < 3; i++) {
			//get endpoints of edges
			int edgeIndex = triangleTable[lookupIndex][nextVertIndex + i];
			float3 vert1 = minCornerPos + edgeVertexOffsets[edgeIndex][0];
			float3 vert2 = minCornerPos + edgeVertexOffsets[edgeIndex][1];

			//interpolate:
			float val1 = samplePoint(vert2);
			float val2 = samplePoint(vert1);

			float diff;
			if ((surfaceLevel > val1 && surfaceLevel > val2) || (surfaceLevel < val1 && surfaceLevel < val2) || val1 == val2) {
				diff = 0.5f;
			}
			else {
				diff = (surfaceLevel - val1) / (val2 - val1);
			}

			float3 mid = vert2 + diff * (vert1 - vert2);

			//mid *= worldZoom; //for zoom, but wrong sectors

			mid += offsetVec;
						
			if (i == 0) {
				tri.vertexA = mid;
			} else if (i == 1) {
				tri.vertexB = mid;
			}
			else {
				tri.vertexC = mid;
			}
		}

		triangles.Append(tri);
		numTris.IncrementCounter();
		nextVertIndex += 3;
	}
}
